plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'org.ajoberstar.grgit' version '3.0.0'
}

import org.ajoberstar.grgit.Grgit
import org.apache.tools.ant.filters.ReplaceTokens

String majorVersion = 3
String minorVersion = 7
String minecraftVersion = "1.14.4"
Grgit repo = Grgit.open(currentDir: project.projectDir)

String branchArtifactId = "rpgitems"

String branch = System.getenv("TRAVIS_BRANCH") == null ? repo.branch.current().name : System.getenv("TRAVIS_BRANCH")
boolean mainBranch = System.getenv("MAIN_BRANCH") == null ? false : Boolean.parseBoolean(System.getenv("MAIN_BRANCH"))
String commit = repo.head().abbreviatedId
if (minecraftVersion != branch) {
    branchArtifactId += branch.replace(minecraftVersion, "")
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations.all {
//    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    // mavenLocal()
    jcenter()
    mavenCentral()
    maven { name 'Spigot'; url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { name 'Sonatype'; url 'https://oss.sonatype.org/content/groups/public' }
    maven { name 'sk89q'; url 'http://maven.sk89q.com/artifactory/repo/' }
    maven { name 'vault-repo'; url 'https://jitpack.io' }
    maven { name 'NyaaCat'; url 'https://ci.nyaacat.com/maven/' }
    maven { name 'EssentialsX'; url 'https://ci.ender.zone/plugin/repository/everything' }
}

shadowJar {
    configurations = [project.configurations.shadow]
}

dependencies {
    // compileOnly files('lib/spigot-1.13.2.jar')
    // compileOnly files('lib/patched_1.13.2.jar')
    api "org.spigotmc:spigot-api:$minecraftVersion-R0.1-SNAPSHOT"
    if (gradle.hasProperty("useLocalDependencies") && gradle.useLocalDependencies) {
        api project(":NyaaCore")
        api project(":LanguageUtils")
    } else {
        api "cat.nyaa:nyaacore:7.0-SNAPSHOT"
    }

    compileOnly 'io.netty:netty-all:4.1.25.Final' // netty is shadowed inside spigot jar
    compileOnly 'org.ow2.asm:asm:7.0'
    compileOnly 'cglib:cglib-nodep:3.2.12'
    shadow 'cglib:cglib-nodep:3.2.12'

    // implementation files('lib/worldedit-bukkit-7.0.0-SNAPSHOT-dist.jar')
    // implementation files('lib/worldguard-legacy-7.0.0-SNAPSHOT-dist.jar')

//    implementation('cat.nyaa:EffectLib:nyaa-v1.0')

    implementation('com.sk89q.worldguard:worldguard-core:7.0.1-SNAPSHOT')
    implementation('com.sk89q.worldguard:worldguard-bukkit:7.0.1-SNAPSHOT') {
        exclude group: 'io.papermc', module: 'paperlib'
        exclude group: 'org.bstats', module: 'bstats-bukkit'
        exclude group: 'org.bukkit', module: 'bukkit'
    }

    implementation 'com.google.code.findbugs:jsr305:1.3.9'
    implementation('net.milkbowl.vault:VaultAPI:1.7') {
        transitive = false
    }
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.forkOptions.jvmArgs += ["-Duser.language=en"]
}

String fullVersionString = System.getenv("FULL_VERSION_STRING")
boolean releasing = fullVersionString == null ? false : Boolean.parseBoolean(System.getenv("RELEASING"))

tasks.withType(ProcessResources) {
    with copySpec {
        filteringCharset = 'UTF-8'
        from 'src/main/resources/lang'
        into 'lang'
        filter(ReplaceTokens, tokens: ['GIT_BRANCH': branch, 'GIT_HASH': commit], beginToken: '{', endToken: '}')
    }
}

publishing {
    publications {
        mavenSnapshot(MavenPublication) {
            from components.java
            afterEvaluate {
                artifacts = [jar, apiJar, javadocJar, sourcesJar]
            }
            groupId "cat.nyaa"
            artifactId branchArtifactId
            version "$majorVersion.$minorVersion-mc$minecraftVersion-SNAPSHOT"
        }
        mavenRelease(MavenPublication) {
            from components.java
            afterEvaluate {
                artifacts = [jar, apiJar, javadocJar, sourcesJar]
            }
            groupId "cat.nyaa"
            artifactId branchArtifactId
            version "$fullVersionString"
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}

tasks.withType(PublishToMavenRepository) {
    onlyIf {
        (publication == publishing.publications.mavenSnapshot) ||
                (releasing && mainBranch && (publication == publishing.publications.mavenRelease))
    }
}

jar {
    classifier = 'core'
    dependsOn shadowJar
}

shadowJar {
    classifier = 'release'
    // relocate 'net.sf.cglib', 'cat.nyaa.cglib'
    configurations = [project.configurations.shadow]
}

task apiJar(type: Jar) {
    includeEmptyDirs = false
    from sourceSets.main.output.classesDirs
    include('**/think/rpgitems/Events.class')
    include('**/think/rpgitems/api/')
    include('**/think/rpgitems/item/')
    include('**/think/rpgitems/power/')
    exclude('**/think/rpgitems/power/impl/')
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    (options as StandardJavadocDocletOptions).with {
        links 'https://docs.oracle.com/javase/8/docs/api/'
        links 'https://hub.spigotmc.org/javadocs/spigot/'
        links 'https://google.github.io/guava/releases/21.0/api/docs/'
        links 'https://nyaacat.github.io/NyaaCentral/NyaaCore/6.3-mc1.13.2/javadoc/'
        links 'https://ci.md-5.net/job/BungeeCord/ws/chat/target/apidocs/'
        links 'https://netty.io/4.1/api/'

        locale 'en_US'
        encoding 'UTF-8'
        docEncoding 'UTF-8'
        addBooleanOption('keywords', true)

        def currentJavaVersion = JavaVersion.current()
        if (currentJavaVersion > JavaVersion.VERSION_1_9) {
            options.addBooleanOption('html5', true)
        }

        windowTitle = "RPGItems Javadoc"
        String fullVersion = System.getenv("FULL_VERSION_STRING")
        if (fullVersion != null) {
            if (minecraftVersion != branch) {
                fullVersion += " (" + branch + ")"
            }
            docTitle = "<b>RPGItems</b> " + fullVersion
        } else {
            docTitle = "<b>RPGItems</b>"
        }
    }
}